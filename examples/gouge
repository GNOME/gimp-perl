#!/usr/bin/perl

# implements some algorithms described in
# http://www.biocomputer.com/Thesis.html

use Gimp::Feature 'pdl';
use Gimp 1.098;
use Gimp::Fu;
use Gimp::Util;
use PDL::LiteF;

sub iterate {
   my ($drawable,$message,$kernel)=@_;

   Gimp->progress_init ($message);

   my @bounds = $drawable->mask;
   my @off = $drawable->offsets;
   $bounds[2]-- if $bounds[0]+$bounds[2] >= ($drawable->offsets)[0]+$drawable->width;
   $bounds[3]-- if $bounds[1]+$bounds[3] >= ($drawable->offsets)[1]+$drawable->height;
   {
      my $src = new PixelRgn ($drawable->get,@bounds[0,1],$bounds[2]+1,$bounds[3]+1,0,0);
      my $dst = new PixelRgn ($drawable->get,@bounds,1,1);

      my $bpp = $src->bpp > 1 ? ":," : "";

      my $iter = Gimp->pixel_rgns_register ($dst);
      my $area = $bounds[2]*$bounds[3];
      my $progress = 0;

      do {
         my ($x,$y,$w,$h)=($dst->x,$dst->y,$dst->w,$dst->h);
         $dst->data($kernel->($bpp,$src->get_rect($x,$y,$w+1,$h+1)->convert(short)));
         $progress += $w*$h/$area;
         Gimp->progress_update ($progress);
      } while (Gimp->pixel_rgns_process ($iter));
   }
   Gimp->progress_update (1);

   $drawable->merge_shadow (1);
   $drawable->update (@bounds);

   ();
}

register "gouge_smooth",
         "smooth (low pass filter) an image using the gouge algorithm",
         "Low-Pass filtering (smoothing) using a fast algorithm found in a paper by James O. Gouge.",
         "Marc Lehmann",
         "Marc Lehmann <pcg\@goof.com>",
         "19990723",
         "<Image>/Filters/Blur/Gouge",
         "RGB*, GRAY*",	
         [],
         sub {
   my($image,$drawable)=@_;

   iterate $drawable,
           "Gouge smoothing...",
           sub {
              ($_[1]->slice("$_[0]0:-2,0:-2")+
               $_[1]->slice("$_[0]1:-1,0:-2")+
               $_[1]->slice("$_[0]1:-1,1:-1")+
               $_[1]->slice("$_[0]0:-2,1:-1"))>>2;
           };
};

register "gouge_contrast",
         "contrast enhance an image using the gouge algorithm",
         "Contrast Enhance an image using a fast algorithm found in a paper by James O. Gouge.",
         "Marc Lehmann",
         "Marc Lehmann <pcg\@goof.com>",
         "19990723",
         "<Image>/Filters/Enhance/Contrast",
         "RGB*, GRAY*",	
         [],
         sub {
   my($image,$drawable)=@_;

   iterate $drawable,
           "Gouge contrast enhancing...",
           sub {
              my $T = $_[1]->slice("$_[0]0:-2,0:-2");
              my $D = $_[1]->slice("$_[0]1:-1,1:-1");

              (($T<<1)-$D)->clip(0,255);
           };
};

register "gouge_edge",
         "detect edges in an image using the gouge algorithm",
         "Detect edges in the image using a fast algorithm found in a paper by James O. Gouge. It is similar to Sobel, yet sharper.",
         "Marc Lehmann",
         "Marc Lehmann <pcg\@goof.com>",
         "19990723",
         "<Image>/Filters/Edge-Detect/Gouge",
         "RGB*, GRAY*",	
         [],
         sub {
   my($image,$drawable)=@_;
   my $bpp = 3-!!($drawable->gray);

   iterate $drawable,
           "Gouge edge detection...",
           sub {
              my $T = $_[1]->slice("$_[0]0:-2,0:-2");
              my $R = $_[1]->slice("$_[0]1:-1,0:-2");
              my $D = $_[1]->slice("$_[0]1:-1,1:-1");

              255-abs(cat($T-$R,$T-$D))
                  ->convert(byte)
                  ->mv($bpp,0)
                  ->maximum;
           };
};

exit main;










