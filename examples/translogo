#!/usr/bin/perl

use Gimp;
use Gimp::Fu;
use Gimp::Util;
use warnings;
use strict;

podregister {
  Gimp::Context->push();
  $drawable->has_alpha or die __"You can't run this script without an ALPHA CHANNEL!!";
  if ($drawable->type_with_alpha == INDEXEDA_IMAGE) {
    die __"You can't run this script with an INDEXED image!!";
  } elsif ($drawable->type_with_alpha == GRAYA_IMAGE) {
    $image->convert_rgb
  }
  my $img = Gimp::Image->new(100, 100, RGB_IMAGE);
  $drawable->get_image->selection_all;
  $drawable->edit_copy;
  $image->selection_none;
  my $layer = $img->layer_new($drawable->width,$drawable->height,$image->layertype(1), __"Text", 100, NORMAL_MODE);
  $layer->drawable_fill(TRANSPARENT_FILL);
  $img->insert_layer($layer,0,0);
  $layer->edit_paste(0)->floating_sel_anchor;
  $img->resize($drawable->width,$drawable->height, 0, 0);
  $img->select_item(CHANNEL_OP_REPLACE, $layer);
  $img->selection_invert;
  Gimp::Context->set_background ([255, 255, 255]);
  $layer->edit_fill(BACKGROUND_FILL);
  $img->selection_none;
  $layer->invert;
  $layer->gauss_rle(2.0, 1, 1);
  my $bump_lay = $img->layer_new($img->width,$img->height, RGBA_IMAGE, __"Bumpmap", 100, NORMAL_MODE);
  $bump_lay->drawable_fill(BACKGROUND_FILL);
  $img->insert_layer($bump_lay,0,0);
  if ($color_or_pattern == 1) {
    Gimp::Context->set_pattern ($pattern);
    $bump_lay->edit_bucket_fill(PATTERN_BUCKET_FILL, NORMAL_MODE, 100, 0, 0, 0, 0);
  } else {
    Gimp::Context->set_background ($txt_col);
    $bump_lay->edit_fill(BACKGROUND_FILL);
  }
  $bump_lay->bump_map($layer, 110.0, 45.0, 4, 0, 0, 0, 0, 1, 0, 0);
  my $pattern_mask = $bump_lay->create_mask(ADD_ALPHA_MASK);
  $bump_lay->add_mask($pattern_mask);
  $img->selection_all;
  $layer->edit_copy;
  $pattern_mask->edit_paste(0)->floating_sel_anchor;
  $pattern_mask->levels(0, 0, 77, 0.91, 0, 255);
  $bump_lay->remove_mask(MASK_APPLY);
  $layer->invert;
  $img->flatten;
  $img->convert_indexed(0, MAKE_PALETTE, 256, 0, 0, "");
  my $new = $img->get_active_layer;
  $new->add_alpha;
  $img->select_color(CHANNEL_OP_ADD, $new, [255, 255, 255]);
  $new->edit_clear;
  $img->selection_none;
  eval {
   Gimp::Display->new($img);
  };
  return $img;
};
exit main;
__END__

=head1 NAME

make_trans_logos - A script to get transparent beveled logos

=head1 SYNOPSIS

<Image>/Filters/Web/Transparent Logo...

=head1 DESCRIPTION

This script aims to produce a transparent logo in an indexed image
which is ready to be saved in .gif format. If you need a transparent
image containing a logo for your web page, just apply this script over
a drawable with an alpha channel in RGB or GRAY mode.

You can choose between colour and pattern fill of your text.

As an input, you need to have an image that has an alpha channel,
and isn't 100% opaque - it's really designed to work with a text layer.
It will then make a "logo" out of your non-white sections of the image
and index it.  The original image is untouched.

=head1 PARAMETERS

 [PF_RADIO, 'color_or_pattern', 'Use a colour or a pattern for text', 0,
    [Colour => 0, Pattern => 1]],
 [PF_PATTERN, 'pattern', "Text pattern", "Wood #1"],
 [PF_COLOUR, 'txt_col', "Text color", [69, 88, 211]],

=head1 RETURN VALUES

 [PF_IMAGE,	'image', "Logo"],

=head1 IMAGE TYPES

RGBA

=head1 HISTORY

  12/5/03: <sjburges@gimp.org>
   s/Gimp->convert/Gimp->image_convert/

=head1 AUTHOR

Michele Gherlone <michele.gherlone@liceoberchet.it>

=head1 DATE

20140526

=head1 LICENSE

(c) 2014 M. Gherlone

Distributed under the same terms as Gimp Perl.
